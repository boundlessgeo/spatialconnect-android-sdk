apply plugin: 'com.android.library'
apply plugin: 'maven'

group 'com.boundlessgeo.spatialconnect'
version '0.3.0'

def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true
        // http://developer.android.com/tools/building/multidex.html#testing
        testInstrumentationRunner "com.boundlessgeo.spatialconnect.test.MultiDexAndroidJUnitRunner"
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                repository url: 'file://' + new File(System.getProperty('user.home'), '.m2/repository')
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
    sourceSets {
        main.jniLibs.srcDirs = ['libs']
    }
    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.google.android.gms:play-services-maps:7.8.0'
    compile 'com.android.support:multidex:1.0.0'
    compile "com.bedatadriven:jackson-datatype-jts:2.2"
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'commons-io:commons-io:2.4'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.facebook.react:react-native:+'
    compile('io.jeo:jeo:0.6') {
        exclude group: 'com.vividsolutions'
        exclude group: 'io.jeo', module: 'proj4j'
    }
    // testing dependencies
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
}

